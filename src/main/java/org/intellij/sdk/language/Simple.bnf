{
  parserClass="org.intellij.sdk.language.parser.SimpleParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Simple"
  psiImplClassSuffix="Impl"
  psiPackage="org.intellij.sdk.language.psi"
  psiImplPackage="org.intellij.sdk.language.psi.impl"

  elementTypeHolderClass="org.intellij.sdk.language.psi.SimpleTypes"
  elementTypeClass="org.intellij.sdk.language.psi.SimpleElementType"
  tokenTypeClass="org.intellij.sdk.language.psi.SimpleTokenType"

  psiImplUtilClass="org.intellij.sdk.language.psi.impl.SimplePsiImplUtil"

  tokens=[
    LBRACE = '{'
    RBRACE = '}'
    LPAREN = '('
    RPAREN = ')'
    LBRACK = '['
    RBRACK = ']'

    SEMICOLON = ';'
    COLON = ':'
    COMMA = ','
    DOT = '.'
    DOUBLE_DOT = '..'

    LESS_THAN = '<'
    GREATER_THAN = '>'

    ASSIGN = "="
    EQUAL = "=="

    SEMICOLON_SYNTHETIC = '<NL>'
    TYPE_ = 'type'
  ]

  generateTokenAccessors=true
}

//program ::= identifier '.' Expression semi
program ::= TopLevelDeclaration*

private semi ::= SEMICOLON_SYNTHETIC | SEMICOLON | <<eof>>

private TopLevelDeclaration ::= !<<eof>> TopDeclaration semi {pin=1 recoverWhile=TopLevelDeclarationRecover}
private TopDeclaration ::=
      TypeDeclaration
    | Function
private TopLevelDeclarationRecover ::= !(SEMICOLON | TYPE_ | func)

VariableName ::= identifier
TypeName ::= identifier
{
    mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
    implements="org.intellij.sdk.language.psi.SimpleNamedElement"
    methods=[getKey getName setName getNameIdentifier getPresentation getReference]
}

Parameters ::= LPAREN (identifier COLON identifier COMMA?)* RPAREN
Arguments ::= LPAREN (Literal COMMA?)* RPAREN
Generic ::= LESS_THAN TypeName GREATER_THAN

TypeDeclaration ::= TYPE_ TypeName Block
{
    mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
    implements="org.intellij.sdk.language.psi.SimpleNamedElement"
    methods=[getKey getName setName getNameIdentifier getPresentation getReference]
}
Function ::= func Generic? identifier Parameters TypeName? Block
{
    mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
    implements="org.intellij.sdk.language.psi.SimpleNamedElement"
    methods=[getKey getName setName getNameIdentifier getPresentation getReference]
}

Block ::= LBRACE Statement* RBRACE

Statement ::= (
      ReturnStatement
    | VariableDeclaration
    | ConstructorCall
    | FunctionCall
    | MemberAccessExpr
) semi

private Literal ::=
      int
    | identifier

Expression ::=
      FunctionCall
    | ConstructorCall
    | MemberAccessExpr
    | BaseExpression

BaseExpression ::= identifier
                | Literal

ReturnStatement ::= return Expression? // return expr
VariableDeclaration ::= identifier (
      COLON TypeName ASSIGN Expression   // a: T = value
    | COLON TypeName                     // a: T
    | ASSIGN Expression)                 // a = value
{
    mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
    implements="org.intellij.sdk.language.psi.SimpleNamedElement"
    methods=[getKey getName setName getNameIdentifier getPresentation getReference]
}
ConstructorCall ::= new TypeName Arguments // new T(...)
{
    mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
    implements="org.intellij.sdk.language.psi.SimpleNamedElement"
    methods=[getKey getName setName getNameIdentifier getPresentation getReference]
}
FunctionCall ::= identifier Arguments // func(...)
{
    mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
    implements="org.intellij.sdk.language.psi.SimpleNamedElement"
    methods=[getKey getName setName getNameIdentifier getPresentation getReference]
}

MemberField ::= identifier
{
    mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
    implements="org.intellij.sdk.language.psi.SimpleNamedElement"
    methods=[getKey getName setName getNameIdentifier getPresentation getReference]
}
MemberAccessExpr ::= MemberField '.' Expression
{
    mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
    implements="org.intellij.sdk.language.psi.SimpleNamedElement"
    methods=[getKey getName setName getNameIdentifier getPresentation getReference]
}